buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

if (System.getenv().BUILD_NUMBER) // assuming jenkins ofcourse
{
    project.buildnumber = System.getenv().BUILD_NUMBER
    println "Using build number ${project.buildnumber}..."
}
else
{
    println "No build number set!"
}

version = "1.3.2-build-" + project.buildnumber
group = "maven.jurassicraft"
archivesBaseName = "JurassiCraft"

minecraft {
    version = "1.7.10-10.13.2.1230"
    runDir = "minecraft"
    replace '${version}', project.version
}

processResources {
	inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'changelog.info'
        include 'json/*.json'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'changelog.info'
        exclude 'json/*.json'
    }
}

jar {
    classifier = 'universal'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

artifacts {
    archives sourcesJar, javadocJar, devJar
}
